plugins {
    id 'fabric-loom' version '1.9-SNAPSHOT'
    id 'maven-publish'
}

version = "${project.mod_version}+${project.minecraft_version}"
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    maven {
        url = uri('https://mschae23.de/git/api/packages/mschae23/maven')
    }

    maven {
        name = 'GitHubPackages'
        url = uri('https://maven.pkg.github.com/mschae23/codec-config-api')
        credentials {
            username = project.findProperty('gpr.user') ?: System.getenv('GITHUB_USERNAME')
            password = project.findProperty('gpr.key') ?: System.getenv('GITHUB_TOKEN')
        }
    }

    // Repositories for the dependencies of Tax Free Levels
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://maven.terraformersmc.com/releases" }
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }

    // maven { url 'https://jitpack.io' }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:$project.minecraft_version"
    mappings "net.fabricmc:yarn:$project.yarn_mappings:v2"
    modImplementation "net.fabricmc:fabric-loader:$project.loader_version"

    [
        "fabric-api-base",
        "fabric-registry-sync-v0",
        "fabric-lifecycle-events-v1",
        "fabric-networking-api-v1"
    ].forEach { modImplementation fabricApi.module(it, project.fabric_api_version) }

    modCompileOnly "io.github.fourmisain:TaxFreeLevels-fabric:$project.tax_free_levels_version"

    modImplementation "de.mschae23:codec-config-api:$project.codec_config_api_version"
    include "de.mschae23:codec-config-api:$project.codec_config_api_version"
}

loom {
    accessWidenerPath = file 'src/main/resources/grindenchantments.accesswidener'
    
    decompilers {
        vineflower {
            options.put("ind", "    ")
            options.put("nls", "1")
        }
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("COPYING")
    from("COPYING.LESSER")
}

// configure the maven publication
publishing {
    publications {
        mod(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name = "Forgejo"
            url = uri("https://mschae23.de/git/api/packages/mschae23/maven")

            credentials(HttpHeaderCredentials) {
                name = "Authorization"
                value = "token ${System.getenv("FORGEJO_PACKAGES_TOKEN")}"
            }

            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
}
